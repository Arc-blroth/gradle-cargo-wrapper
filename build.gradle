plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.3.1'
}

group = 'io.github.arc-blroth'
version = "1.1.0"

base {
    archivesName = 'cargo-wrapper'
}

dependencies {
    compileOnly gradleApi()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

gradlePlugin {
    website = "https://$pluginUrlBase"
    vcsUrl = "https://${pluginUrlBase}.git"

    plugins {
        cargoWrapper {
            id = 'io.github.arc-blroth.cargo-wrapper'
            implementationClass = 'ai.arcblroth.cargo.CargoWrapperPlugin'
            displayName = pluginDisplayName
            description = pluginDescription
            tags = ['rust', 'cargo', 'native']
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            forEach {
                if(it instanceof MavenPublication) {
                    it.pom {
                        url = "https://$pluginUrlBase"
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'arcblroth'
                                name = "Arc'blroth"
                            }
                        }
                        scm {
                            def scmRepo = "scm:git:git://${pluginUrlBase}.git"
                            connection = scmRepo
                            developerConnection = scmRepo
                            url = scmRepo
                        }
                    }
                }
            }
        }
    }
}
